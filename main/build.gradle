plugins {
    id 'maven-publish'
    id "com.github.johnrengelman.shadow" version "7.1.2"
}

ext {
    core = "4.1.29"
    minigameCore = "1.0.1"
}

java {
    withJavadocJar()
    withSourcesJar()
}

javadoc {
    options.links = [
            "https://javadoc.minestom.net/",
            "https://hsgamer.github.io/MinigameCore/",
            "https://hsgamer.github.io/HSCore/",
            "https://jd.adventure.kyori.net/api/4.11.0/"
    ]
}

shadowJar {
    archiveBaseName.set(rootProject.name)
    exclude 'extension.json'
}

artifacts {
    archives shadowJar
    shadow shadowJar
}

publishing {
    publications {
        java(MavenPublication) {
            artifactId = rootProject.name
            pom {
                name = rootProject.name
                withXml {
                    def repositories  = asNode().appendNode("repositories")
                    project.repositories.each {
                        if (it instanceof MavenArtifactRepository) {
                            if (it.name == "MavenLocal" || it.name == "MavenRepo") {
                                return
                            }
                            def repository = repositories.appendNode("repository")
                            repository.appendNode("id", it.name)
                            repository.appendNode("url", it.url)
                        }
                    }
                }
            }
            from components.java
        }
    }
}

dependencies {
    api "me.hsgamer:hscore-config-configurate:$core"
    api "me.hsgamer:hscore-config-proxy:$core"
    api 'org.spongepowered:configurate-yaml:4.1.2'

    api "me.hsgamer:hscore-builder:$core"
    api "me.hsgamer:hscore-common:$core"
    api "me.hsgamer:hscore-collections:$core"
    api "me.hsgamer:hscore-minestom-board:$core"
    api "me.hsgamer:hscore-minestom-gui:$core"

    api "me.hsgamer:minigamecore-base:$minigameCore"
    api "me.hsgamer:minigamecore-implementation:$minigameCore"

    api "com.github.Minestom.Minestom:Minestom:1a013728fd"
    api "com.github.TogAr2:MinestomPvP:880e5cd71c"
}

jar {
    manifest {
        attributes 'Main-Class': 'me.hsgamer.flexegames.FlexEGames', "Multi-Release": true
    }
}