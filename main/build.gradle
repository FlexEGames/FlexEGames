plugins {
    id 'maven-publish'
    id "com.github.johnrengelman.shadow" version "7.1.2"
}

ext {
    core = "4.1.28"
    minigameCore = "1.0.1"
}

java {
    withJavadocJar()
    withSourcesJar()
}

javadoc {
    options.links = [
            "https://javadoc.minestom.net/",
            "https://hsgamer.github.io/MinigameCore/",
            "https://hsgamer.github.io/HSCore/",
            "https://jd.adventure.kyori.net/api/4.11.0/"
    ]
}

shadowJar {
    archiveClassifier.set(null)
    archiveBaseName.set(rootProject.name)
    exclude 'extension.json'
}

artifacts {
    archives shadowJar
    shadow shadowJar
}

publishing {
    publications {
        shadow(MavenPublication) { publication ->
            artifactId = rootProject.name
            pom {
                name = rootProject.name
            }

            project.shadow.component(publication)
            artifact sourcesJar
            artifact javadocJar
        }
    }
}

dependencies {
    api "me.hsgamer:hscore-config-configurate:$core"
    api "me.hsgamer:hscore-config-proxy:$core"
    api 'org.spongepowered:configurate-yaml:4.1.2'

    api "me.hsgamer:hscore-builder:$core"
    api "me.hsgamer:hscore-common:$core"
    api "me.hsgamer:hscore-collections:$core"
    api "me.hsgamer:hscore-minestom-board:$core"
    api "me.hsgamer:hscore-minestom-gui:$core"

    api "me.hsgamer:minigamecore-base:$minigameCore"
    api "me.hsgamer:minigamecore-implementation:$minigameCore"

    api "com.github.Minestom.Minestom:Minestom:93718276c5"
    api "com.github.HSGamer:MinestomPvP:0643be2072"
}

jar {
    manifest {
        attributes 'Main-Class': 'me.hsgamer.flexegames.FlexEGames', "Multi-Release": true
    }
}